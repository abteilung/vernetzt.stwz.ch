    prototype(Abte.Site:Component.Footer) < prototype(Neos.Neos:ContentComponent) {
        
        siteNode = ${site}

        year = ${Date.format(Date.now(), 'Y')}
        
        footerInfo = Neos.Neos:Editable {
            node = ${site}
            property = 'footerInfo'
        }

        footerContent = Neos.Neos:ContentCollection {
            nodePath = ${ q(site).children('footer').property('_path') }
            collection = ${ q(site).children('footer').children() }
        }
        
        metaItems = Stwz.Navigation:Component.Nav.Meta {
            menuItems {
                itemCollection >
                startingPoint = ${ q(site).children('[uriPathSegment="meta"]').get(0) }
            }
        }

        title = Neos.Neos:Editable {
            property = 'title'
        }

        address = Neos.Neos:Editable {
            property = 'address'
            node = ${ site }
        }

        contacts = Neos.Neos:Editable {
            property = 'contacts'
        }


        phone = ${ q(site).property('helpdeskPhone') }
        
        email = ${ q(site).property('helpdeskEmail') }
        
        web = ${ q(site).property('helpdeskWeb') }

        renderer = afx`

            <footer class="relative z-10">

                <div class="pt-8 pb-16 bg-white">
                    <div class={q(node).is('[instanceof Abte.Site:Document.ComingSoon]') ? 'container pt-4' :'container border-black pt-4 border-t-[1px]' }>
                        <div class="grid text-sm gridPadding-x md:grid-cols-4 lg:grid-cols-6">
                            <div>
                                {props.address}
                            </div>
                            <div>
                                <Carbon.Link:Link 
                                    additionalClass="hover:underline font-extrabold" 
                                    link={'tel:' + String.replace(String.pregReplace(props.phone, "'/[^0-9]/'", ''), ' ', '')} 
                                    renderDefaultTagIfNoLink='true'
                                >
                                    <Neos.Neos:Editable property="helpdeskPhone" node={site} />
                                </Carbon.Link:Link>
                                <br/>
                                <Carbon.Link:Link 
                                    additionalClass="hover:underline font-extrabold" 
                                    link={'mailto:' + props.email} 
                                    renderDefaultTagIfNoLink='true'
                                >
                                    <Neos.Neos:Editable property="helpdeskEmail" node={site} />
                                </Carbon.Link:Link>
                                <br/>

                                <Carbon.Link:Link 
                                    additionalClass="hover:underline font-extrabold" 
                                    link={'https://' + props.web} 
                                    renderDefaultTagIfNoLink='true'
                                >
                                    <Neos.Neos:Editable property="helpdeskWeb" node={site} />
                                </Carbon.Link:Link>
                            </div>
                            <div>
                                {props.metaItems}
                                Â© {props.year} {props.footerInfo}
                            </div>


                            <div class="flex justify-end lg:col-span-3">
                                <Neos.Neos:NodeLink 
                                    node={props.siteNode} 
                                    >
                                    <Abte.Site:Component.StWZLogo class="w-auto h-12" />
                                </Neos.Neos:NodeLink>

                            </div>
                        </div>

                    </div>
                </div>

            </footer>
        `

        @cache {
            mode = 'cached'
            entryIdentifier {
                site = ${site}
            }
            entryTags {
                1 = ${Neos.Caching.nodeTag(site)}
            }
        }
        
    }
