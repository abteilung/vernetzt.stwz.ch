prototype(Abte.Site:Component.HomepageHeader) < prototype(Neos.Fusion:Component) {
    
    teaserText = Neos.Neos:Editable {
        property = 'teaser_short'
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    subTitle = Neos.Neos:Editable {
        property = 'subTitle'
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    svgIllustration = Neos.Fusion:Tag {
        attributes.class = 'lineAnimation'
        content = ${documentNode.properties.illustration}
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
    }

    hideHeader = ${ q(documentNode).property('hideHeader') }

    ##
    # If you want to use a default rendering for a menu, you can just use:
    ##
    # <Neos.Neos:Menu
    #     startingPoint={site}
    #     attributes.class="navigation"
    #     normal.attributes.class="navigation-item"
    #     active.attributes.class="navigation-item navigation-item--state-active"
    #     current.attributes.class="navigation-item navigation-item--state-current"
    #     />
    ##


    productIcons = Neos.Fusion:Loop {
        items = ${ q(site).children('[instanceof Abte.Site:Document.Product]').get() }
        itemRenderer = Neos.Neos:NodeLink {
            node = ${ item }
            content = Abte.Site:Mixin.Icons {
                node = ${ q(item) }
                class="scale-90 hover:scale-100 duration-150"
            }
        }
        @process.template = ${ '<div class="flex space-x-4">' + value + '</div>'}
    }

    renderer = afx`
        <header class={pageClass + ' ' + 'relative bg-cover bg-fixed bg-[url("Media/Backdrops/MainBackdrop.svg")]' }>
            <div class="grid grid-cols-5 gridgap-x flex justify-between h-full items-center container relative h-[350px] md:h-[450px] lg:h-[500px] relative top-0 z-20 w-full mt-0 overflow-visible xl:mb-10">
                <div class="space-y-10 col-span-2">
                    <Neos.Fusion:Augmenter class="h1 text-black">
                        {props.teaserText}
                    </Neos.Fusion:Augmenter>
                    {props.productIcons}
                    <Abte.Site:Content.Button class="scale-100 hover:scale-105 duration-150 text-white bg-black" text="Angebot" iconAfter="" target="#id-node-yidfj5izddfnx" />
                </div>
                <div class="col-span-3 h-full illustrationContainer">
                    {props.svgIllustration}
                </div>
            </div>
            <div class="absolute right-0 z-0 -bottom-40">
                <Stwz.MovingLines:Component.Lines />
            </div>
        </header>
    `

	@cache {
		mode = 'cached'
		entryIdentifier {
			documentNode = ${documentNode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
		}
	}
}
