
prototype(Abte.Site:Content.Accordion.Item) < prototype(Neos.Neos:ContentComponent) {

    header = Neos.Neos:Editable {
        property = 'accordion_title'
    }

    headerClass = 'h1 text-primary space-x-4 flex items-center'

    accordion_title = ${ q(node).property('accordion_title') }
    
    accordion_open = ${ q(node).property('accordion_open') }

	content = Neos.Neos:ContentCollectionRenderer {
		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}

    cycle       = ''
    buttonClass = 'w-full py-2 text-left'
    itemClass   = 'relative border-b-[1px] border-black'
    caretColor  = 'text-black scale-75 lg:scale-100'

    accordion_firstOpen = ${ q(node).closest('[instanceof Abte.Site:Content.Accordion]').property('accordion_firstOpen') ? 'show' : 'collapsed' }


  	renderer = afx`
        <div 
            class={props.itemClass}
        >
            <button
                type="button"
                class={props.buttonClass}
                "@click"={'selected !== ' + i.cycle + ' ? selected = ' + i.cycle + ' : selected = null'}
            >

                <div class="flex items-center justify-between">
                    <div class={props.headerClass}><Abte.Site:Content.Accordion.Icon />{props.header}</div>
                    <div class={props.caretColor}>
                        <Neos.Fusion:Augmenter x-bind:class={'selected == ' + i.cycle + ' ? \'transform duration-300 rotate-180\' : \'\' '}>
                            <Abte.Site:Icon.ChevronDown class="transition" />
                        </Neos.Fusion:Augmenter>
                    </div>
                </div>
            </button>

            <div
                class="relative overflow-hidden transition-all duration-700 max-h-0"
                x-ref={'container' + i.cycle}
                x-bind:style={'selected == ' + i.cycle + ' ? \'max-height: \' + $refs.container' + i.cycle + '.scrollHeight + \'px\' : \'\' '}
            >
                <div
                    class="pb-6 text-primary"
                >
                    {props.content}
                </div>
            </div>
        </div>
    `
}

prototype(Abte.Site:Content.Accordion.Icon)  < prototype(Neos.Fusion:Component) {
    
    node = ${ node }
    @context {
        node = ${ this.node }
    }

    icon = Abte.Site:Mixin.Icons {
        class = 'scale-75 absolute left-0'
    }

    renderer = afx`
        <div class="relative block w-24"
            @if.hasIcon={props.icon}
        >
            {props.icon}
            <Abte.Site:Content.Accordion.IconDivider class="absolute left-0" />
        </div>
    `
}

prototype(Abte.Site:Content.Accordion.IconDivider)  < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <svg xmlns="http://www.w3.org/2000/svg" width="76.024" height="54.482" viewBox="0 0 76.024 54.482">
            <line id="Linie_1017" data-name="Linie 1017" y1="53.667" x2="75.444" transform="translate(0.29 0.407)" fill="none" stroke="#000" stroke-width="1"/>
        </svg>
    `
}

