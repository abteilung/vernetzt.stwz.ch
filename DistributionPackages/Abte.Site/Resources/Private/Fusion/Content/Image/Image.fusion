/**
 * This code is feasable for production, and renders an image with LazyBones.
 *
 * To better learn Neos take a look at Abte.Site:Content.BasicImage,
 * which renders our images as an img tag.
 */
prototype(Abte.Site:Content.Image) < prototype(Neos.Neos:ContentComponent) {

    # if the current node has previous siblings it is not the
	# first content and should be loaded lazy otherwise not
	lazy = ${q(node).prev().is()}

    class = Abte.Site:Component.AdditionalClass {
        class = 'container-image'
    }

    node = ${ node }
    @context {
        node = ${ this.node }
    }

    src = Neos.Fusion:Case {
        image {
            condition = ${q(node).property('image') ? true : false}
            renderer = Sitegeist.Kaleidoscope:AssetImageSource {
                asset = ${q(node).property('image')}
            }
        }
        dummyImage {
            condition = ${site.context.inBackend}
            renderer = Sitegeist.Kaleidoscope:DummyImageSource
        }
    }
    
    alternativeText = ${ q(node).property('alternativeText') }

    renderer = afx`
        <Sitegeist.Kaleidoscope:Image
            class={props.class}
            lazy={true}
            lazyWidth={200}
            imageSource={props.src}
            alt={props.alternativeText}
        />
	`

}
