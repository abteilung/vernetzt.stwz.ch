prototype(Abte.Site:Content.Person) < prototype(Neos.Neos:ContentComponent) {

    node = ${ node }    
    @context {
        node = ${ this.node }
    }

    title = Neos.Neos:Editable {
        property = 'title'
    }

    name = Neos.Neos:Editable {
        property = 'name'
    }

    jobTitle = Neos.Neos:Editable {
        property = 'jobTitle'
    }

    portrait = Sitegeist.Kaleidoscope:Image {
        imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
            asset   = ${ q(node).property('image') }
        }
        width =  800
        height = 800
        class = "w-full h-full aspect-square"
    }


    phone = ${ q(node).property('phone') }
    
    email = ${ q(node).property('email') }

    itemID = ${ String.replace( q(node).property('_nodeName') , 'node-', '') }

    backgroundColor = ${ q(node).property('bgColor') }

    // relative bg-white border rounded-lg shadow-sm p-4 flex cursor-pointer focus:outline-none border-transparent border-indigo-500 ring-2 ring-indigo-500

    renderer = afx`
        <div class="personCard" id={props.itemID}>
            <div class="grid grid-cols-3 gap-4">
                <div class="w-full">
                    <div class="overflow-hidden rounded-full">
                        {props.portrait}
                    </div>
                </div>
                <div class="col-span-2">
                    <h2 class="mb-2 !text-black">{props.title}</h2>
                    
                    <b>{props.name}</b>
                    <p>{props.jobTitle}</p>

                    <div class="relative flex items-center justify-center w-16 aspect-square">
                        <Carbon.Link:Link 
                            additionalClass="hover:underline font-extrabold" 
                            link={'mailto:' + props.email} 
                            renderDefaultTagIfNoLink='true'
                        >
                            <Abte.Site:Icon.Envelope class="absolute top-0 left-0 w-6 duration-150 scale-100 hover:scale-110" />
                        </Carbon.Link:Link>
                        
                        <Abte.Site:Icon.Diagonal />

                        <Carbon.Link:Link 
                            additionalClass="hover:underline font-extrabold" 
                            link={'tel:' + String.replace(String.pregReplace(props.phone, "'/[^0-9]/'", ''), ' ', '')} 
                            renderDefaultTagIfNoLink='true'
                        >
                            <Abte.Site:Icon.Phone class="absolute bottom-0 right-0 w-6 duration-150 scale-100 hover:scale-110" />
                        </Carbon.Link:Link>
                    </div>

                </div>
                
            </div>
        </div>
    `
}