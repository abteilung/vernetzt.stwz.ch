prototype(Abte.Site:Content.Products) < prototype(Neos.Neos:ContentComponent) {
    
    node = ${ node }
    @context {
        node = ${ this.node }
    }
    
    title = ${ q(node).property('title') }

    productGroup = ${ q(node).property('group') }
    @context.productGroup = ${this.productGroup}

    shopItems = Neos.Fusion:Collection {
        collection = ${ q(node).children('[instanceof Abte.Site:Content.ProductCard]') }
        itemName = 'node'
        iterationName = 'i'
        itemRenderer = Abte.Site:Content.ProductCard {
            productGroup = ${productGroup}
            class = 'xxx'
        }
    }

    sliderConfig = Neos.Fusion:DataStructure {
        cellSelector = '.productCard'
        pageDots = false
        contain = true
        draggable = ${ node.context.inBackend ? false : true }
        groupCells = true
        setGallerySize = true
        selectedAttraction = "0.2"
        friction = "0.5"
        // adaptiveHeight = false
        prevNextButtons = true
        arrowShape = ${'M32.38,81.47c1.6,1.6,4.18,1.6,5.78,0,1.6-1.6,1.6-4.18,0-5.78l-20.93-20.93H95.01c2.26,0,4.09-1.83,4.09-4.09,0-2.26-1.83-4.09-4.09-4.09H15.89l22.27-22.27c1.6-1.6,1.6-4.18,0-5.78-.8-.8-1.84-1.2-2.89-1.2s-2.09,.4-2.89,1.2L.91,50l31.47,31.47Z'}
        @process.stringify = ${ Json.stringify(value) }
    }

    renderer = afx`

        <fieldset 
            class="w-full h-full productSlider"
            x-data="radioGroup"
            x-init="init()"
            "@item-reset.window"={"value"+ productGroup +"= localStorage.getItem('"+ productGroup +"') || 0;"}
            data-flickity={props.sliderConfig}
        >
            {props.shopItems}
        </fieldset>
                    
    `
    @cache {
        mode = 'cached'
        entryIdentifier {
            collection = ${node}
        }
        entryTags {
            1 = ${Neos.Caching.descendantOfTag(node)}
            2 = ${Neos.Caching.nodeTag(node)}
        }
        maximumLifetime = ${q(node).context({'invisibleContentShown': true}).children().cacheLifetime()}
    }

}