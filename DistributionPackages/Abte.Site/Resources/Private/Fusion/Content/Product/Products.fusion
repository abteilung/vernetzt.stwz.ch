prototype(Abte.Site:Content.Products) < prototype(Neos.Neos:ContentComponent) {
    
    node = ${ node }
    @context {
        node = ${ this.node }
    }
    
    title = ${ q(node).property('title') }
    subTitle = ${ q(node).parents('[instanceof Abte.Site:Document.Products]').property('title') }

    shopItems = Neos.Fusion:Collection {
        collection = ${ q(node).children('[instanceof Abte.Site:Content.ProductCard]') }
        itemName = 'node'
        iterationName = 'i'
        itemRenderer = Abte.Site:Content.ProductCard
    }

    sliderConfig = Neos.Fusion:DataStructure {
        cellSelector = '.productCard'
        pageDots = false
        contain = true
        draggable = ${ node.context.inBackend ? false : true }
        groupCells = true
        adaptiveHeight = false
        prevNextButtons = true
        @process.stringify = ${ Json.stringify(value) }
    }

    renderer = afx`

        <fieldset 
            class="w-full productSlider"
            x-data="window.Components.radioGroup({ initialCheckedIndex: 0 })"
            x-init="init()"
            data-flickity={props.sliderConfig}
        >
            {props.shopItems}
        </fieldset>
                    
    `
}