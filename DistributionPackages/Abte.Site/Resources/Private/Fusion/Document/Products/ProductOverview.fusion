 prototype(Abte.Site:Document.ProductOverview) < prototype(Abte.Site:Document.AbstractPage) {
    body.content = Neos.Fusion:Component {
        aside = Neos.Neos:ContentCollection {
            nodePath = 'aside'
        }

        pageTitle = Neos.Neos:Editable {
            property = 'titleOverride'
            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @position = 'end'
            }
        }

        subTitle = Neos.Neos:Editable {
            property = 'subTitle'
            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @position = 'end'
            }
        }

        teaser_short = Neos.Neos:Editable {
            property = 'teaser_short'
            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @position = 'end'
            }
        }

        productSiblings = ${ q(documentNode).parent().children('[instanceof Abte.Site:Document.Products]').get() }

        renderer = afx`
            <!-- <header class="pt-32 mb-32">
                
            </header> -->
            <div class="gridPadding">
                <div class="container lg:grid lg:grid-cols-12 lg:gridgap">
                    <main class="w-full space-y-16 lg:col-span-8">
                        <div>
                            <Stwz.Navigation:Component.Nav.Breadcrumb />
                            <h2 class="h1">{props.pageTitle}</h2>
                            <Neos.Fusion:Augmenter class="lead">
                                {props.teaser_short}
                            </Neos.Fusion:Augmenter>
                        </div>

                        <Abte.Site:Component.SubMenuAsCards items={ props.productSiblings } cols="3" />

                        {props.main}

                    </main>
                    <aside class="col-span-12 md:w-1/2 lg:w-full lg:col-span-3 lg:col-end-13">
                        <div class="sticky grid md:grid-cols-2 gridPadding-top top-24 lg:grid-cols-1 gridgap">
                            <Abte.Site:Content.ContactBox />
                            {props.aside}
                        </div>
                    </aside>
                </div>
            </div>
        `
    }
}