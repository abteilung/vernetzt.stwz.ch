prototype(Roen.Navigation:Component.Navbar.Language) < prototype(Neos.Fusion:Component) {
    
    menuItems = Neos.Neos:DimensionsMenuItems {
        dimension = 'language'
        // presets = ${['de', 'en']}
    }

    subNavCaret = Roen.Navigation:Component.SubNavCaret

    renderer = afx`
        <div 
            "@click.away"="open = false" 
            class="relative font-headings" 
            x-data="\{ open: false \}"
        >
            <Neos.Fusion:Loop 
                items={props.menuItems} 
                iterationName='i'
            >
                <Neos.Fusion:Tag
                    tagName="button" 
                    attributes.class="flex py-1 transition text-primary xl:py-2 focus:outline-none focus:ring-0" 
                    attributes."@click"="open = !open"
                    @if.isActiveLang={item.state == "current"}
                >
                    <span class="uppercase">{item.label}</span>
                    <span class="text-primary">
                        {props.subNavCaret}
                    </span>
                </Neos.Fusion:Tag>
            </Neos.Fusion:Loop>

            <div 
                x-show="open" 
                x-transition:enter="transition ease-out duration-100" 
                x-transition:enter-start="transform opacity-0 scale-95" 
                x-transition:enter-end="transform opacity-100 scale-100" 
                x-transition:leave="transition ease-in duration-75" 
                x-transition:leave-start="transform opacity-100 scale-100" 
                x-transition:leave-end="transform opacity-0 scale-95" 
                class="absolute right-0 z-10 w-full origin-top-right"
            >
                <div class="absolute right-0 w-12 mt-2 origin-top-right bg-white rounded-md shadow-lg focus:outline-none">
                    <Neos.Fusion:Loop items={props.menuItems}>
                        <Neos.Neos:NodeLink 
                            @if.isActiveLang={item.state != "current"}
                            attributes.class="block px-4 py-2 text-primary hover:text-primary-focus focus:bg-black focus:outline-none focus:shadow-outline" 
                            node={item.node}
                        >
                            {item.label}
                        </Neos.Neos:NodeLink>
                    </Neos.Fusion:Loop>    
                </div>
            </div>
        </div>
                
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${documentNode}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
    
}